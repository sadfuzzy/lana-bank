config {
	type: "operations",
	hasOutput: true,
	description: "Este archivo almacena la información general de las garantías de depósitos pignorados, que respaldan una o más referencias crediticias.",
	dependencies: ["report_03_npb4_17_referencia_garantia_xml"],
}

CREATE OR REPLACE TABLE FUNCTION ${self()} (asof TIMESTAMP) AS (

	SELECT
		LEFT(REPLACE(UPPER(loan_id), '-', ''), 20) AS `identificacion_garantia`		-- uses the 20 leftmost no-hyphen characters from backend loan_id - loan-to-collateral being 1-to-1
		, LEFT(REPLACE(customer_id, '-', ''), 14) AS `nit_depositante`				-- use NIU type which uses the 14 leftmost no-hyphen characters from backend customer_id
		, DATE(most_recent_collateral_deposit) AS `fecha_deposito`					-- Deposit date.
		, CAST(NULL AS DATE) AS `fecha_vencimiento`									-- Due date of the deposit.
		, (total_collateral * (
			SELECT ANY_VALUE(usd_cents_per_btc HAVING MAX uploaded_at)
			FROM ${ref({
				name: "price_cents_btc",
				schema: envs.currentImportSchema
			})}
		) / 100) AS `valor_deposito`												-- Deposit value.
		, "DE" AS `tipo_deposito`													-- "DE" for cash deposits
		, "BC99" AS `cod_banco`														-- "BC99" for a yet undefined lava bank in `stg_dim_15_npb4_17_emisores_de_avales_fianzas_y_cartas`

	FROM ${ref("stg_approved_loans")}(asof)

	WHERE NOT completed

)
