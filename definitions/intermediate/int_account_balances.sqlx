config {
	type: "operations",
	hasOutput: true,
	tags : ["regulatory-report"],
}

CREATE OR REPLACE TABLE FUNCTION ${self()} (asof TIMESTAMP) AS (

SELECT
	JSON_VALUE(values, "$.account_id") AS account_id,
	JSON_VALUE(values, "$.currency") AS currency,
	CAST(JSON_VALUE(ANY_VALUE(values HAVING MAX recorded_at), "$.settled.cr_balance") AS NUMERIC) AS settled_cr,
	CAST(JSON_VALUE(ANY_VALUE(values HAVING MAX recorded_at), "$.settled.dr_balance") AS NUMERIC) AS settled_dr,

FROM ${ref({
	name: "balances",
	schema: envs.currentImportSchema
})}

WHERE recorded_at < asof

GROUP BY account_id, currency

);
