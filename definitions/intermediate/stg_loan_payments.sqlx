config {
	type: "table",
}

WITH projected_months AS (

	SELECT loan_id, principal, duration_value, completed, annual_rate,
		GENERATE_DATE_ARRAY(DATE(start_timestamp), DATE(end_date), INTERVAL 1 MONTH) AS months

	FROM ${ref("stg_approved_loans")}(CURRENT_TIMESTAMP())

), projected AS (

	SELECT loan_id,  completed, principal, annual_rate,
		o+1 AS payment_number,
		LAST_DAY(projected_month) AS projected_date,
		principal / duration_value AS principal_due,

	FROM projected_months,
		UNNEST(months) AS projected_month
			WITH OFFSET o

), payments AS (

	SELECT id AS loan_id
		, ROW_NUMBER() OVER (PARTITION BY id ORDER BY sequence) AS payment_number
		, recorded_at AS payment_recorded_at
		, CAST(JSON_VALUE(event, "$.interest_amount") AS NUMERIC) AS interest_paid
		, CAST(JSON_VALUE(event, "$.principal_amount") AS NUMERIC) AS principal_paid

	FROM ${ref({
		name: "loan_events",
		schema: envs.currentImportSchema
	})}

	WHERE event_type = "payment_recorded"

), interest AS (

	SELECT id AS loan_id
		, ROW_NUMBER() OVER (PARTITION BY id ORDER BY sequence) AS payment_number
		, recorded_at AS interest_recorded_at
		, CAST(JSON_VALUE(event, "$.amount") AS NUMERIC) AS interest_incurred

	FROM ${ref({
		name: "loan_events",
		schema: envs.currentImportSchema
	})}

	WHERE event_type = "interest_incurred"

), joined AS (

	SELECT loan_id, payment_number,
		projected_date, payment_recorded_at, interest_recorded_at,
		principal_due, principal_paid,
		principal - COALESCE(SUM(principal_paid) OVER (
			PARTITION BY loan_id
			ORDER BY payment_number
			ROWS BETWEEN UNBOUNDED PRECEDING AND CURRENT ROW
		), 0) AS principal_remaining,
		payment_number - MAX(payment_number) OVER (
			PARTITION BY loan_id
		) AS payments_remaining,
		(principal - COALESCE(SUM(principal_paid) OVER (
			PARTITION BY loan_id
			ORDER BY payment_number
			ROWS BETWEEN UNBOUNDED PRECEDING AND CURRENT ROW
		), 0)) * annual_rate/1200 AS interest_due,
		interest_incurred, interest_paid,

	FROM projected
	FULL JOIN payments USING (loan_id, payment_number)
	FULL JOIN interest USING (loan_id, payment_number)

	WHERE interest_recorded_at IS NOT NULL
		OR NOT completed

)

SELECT loan_id, payment_number,
	projected_date, payment_recorded_at, interest_recorded_at,
	principal_due, principal_paid,
	principal_remaining / GREATEST(1, payments_remaining) AS projected_principal_amount,
	interest_due, interest_incurred, interest_paid,

FROM joined

ORDER BY loan_id, payment_number
